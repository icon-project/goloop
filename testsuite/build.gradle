allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.squareup.okhttp3:okhttp:3.11.0"
    }
}

subprojects {
    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs = ['src/java']
        test.java.srcDirs = ['test/java']
    }
}

sourceSets.test.java.srcDirs = ["java"]

dependencies {
    implementation "org.msgpack:jackson-dataformat-msgpack:0.8.17"
    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation project(':sdk')
    implementation project(':api')
    implementation project(':rt')
    implementation project(':tooling')
    implementation project(':samples')
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.0"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.9.0"
    testImplementation files(project(':rt').sourceSets.test.output)
}

compileTestJava {
    options.warnings = true
    options.deprecation = true
}


import okhttp3.MediaType
import okhttp3.OkHttpClient
import okhttp3.Request
import okhttp3.RequestBody
import java.util.concurrent.TimeUnit

class ChainTest extends Test {
    String configFile = "./data/config.json"
    String logFile = ".chain/normal.log"
    String envFile = "./data/env.properties"
    String chainDir = ".chain/normal"
    String gsFile = null
    String gsDir = null
    String dockerEnv = null
    String dbType = null
    Process process = null

    void startServer() {
        List<String> commands = new LinkedList<String>()
        commands.add("../bin/gochain")
        commands.add("--config")
        commands.add(configFile)
        commands.add("--chain_dir")
        commands.add(chainDir)
        if (gsFile != null) {
            commands.add("--genesis_storage")
            commands.add(gsFile)
        } else if (gsDir != null) {
            commands.add("--genesis")
            commands.add(gsDir)
        }
        if (dbType != null) {
            commands.add('--db_type')
            commands.add(dbType)
        }
        def logLevel = System.getProperty("LOG_LEVEL")
        if (logLevel != null && logLevel.length() != 0) {
            commands.add("--log_level=" + logLevel)
        }

        ["rm", "-rf", chainDir].execute(null, getWorkingDir()).waitFor()

        ProcessBuilder pb = new ProcessBuilder(commands)

        // adding PYTHONPATH for using pyee directly.
        def env = pb.environment()
        env.put("PYTHONPATH", "../pyee")

        // set working directory
        pb.directory(getWorkingDir())

        File log_file = new File(getWorkingDir(), logFile)
        pb.redirectError(log_file)
        pb.redirectOutput(log_file)

        process = pb.start()

        // Hook shutdown for kill the process for sure
        Runtime.getRuntime().addShutdownHook(new Thread(new Runnable() {
            void run() {
                stopServer()
            }
        }))
    }

    void startDocker(int serverPort) {
        def glTag = System.getProperty("GL_TAG", "latest")
        List<String> commands = new LinkedList<String>()
        commands.add("docker")
        commands.add("run")
        commands.add("-dit")
        commands.add("-v")
        commands.add(getWorkingDir().toString() + ":" + "/testsuite")
        commands.add("-p")
        commands.add("${serverPort}:${serverPort}")
        commands.add("--env-file")
        commands.add("data/dockerenv/" + dockerEnv)
        if (dbType != null) {
            commands.add("--env")
            commands.add("GOCHAIN_DB_TYPE="+dbType)
        }
        commands.add("--name")
        commands.add("gochain-" + dockerEnv)
        commands.add("goloop/gochain:${glTag}")
        stopContainer(true)
        process = commands.execute(null, getWorkingDir())
        if (process.waitFor() == 0) {
            Runtime.getRuntime().addShutdownHook(new Thread(new Runnable() {
                void run() {
                    stopDocker()
                }
            }))
        } else {
            process = null
            throw new IllegalStateException("FAIL to start container gochain-${dockerEnv}")
        }
    }

    @TaskAction
    @Override
    void executeTests() {
        def noServer = System.getProperty("NO_SERVER")
        if (noServer == null || noServer != "true") {
            def useDocker = System.getProperty("USE_DOCKER", "false")
            dbType = System.getProperty("DB_TYPE")
            logging.println("STARTING Stand-alone server")
            logging.println("Docker: ${useDocker}")
            logging.println("Config: ${configFile}")
            logging.println("Log   : ${logFile}")
            logging.println("Env   : ${envFile}")
            if (gsFile != null) {
                logging.println("GSFile: ${gsFile}")
            }
            if (dbType != null) {
                logging.println("DBType: ${dbType}")
            }
            if (useDocker != null && useDocker == "true") {
                def uri = getServerURI()
                startDocker(uri.port)
            } else {
                startServer()
            }
            systemProperty("CHAIN_ENV", envFile)

            def uri = getServerURI()
            if (!waitServer("${uri}/api/v3")) {
                throw new Exception('FAIL to start server')
            }
        } else {
            systemProperty("CHAIN_ENV", System.getProperty("CHAIN_ENV"))
        }
        try {
            super.executeTests()
        } finally {
            stopServer()
            stopDocker()
        }
    }

    private URI getServerURI() {
        def envProps = new Properties()
        envProps.load(new File(getWorkingDir(), envFile).newReader())
        def url = envProps.get('node0.url') as String
        if (url == null) {
            throw new IllegalStateException("${envFile} doesn't have node0.url")
        }
        return new URI(url)
    }

    /**
     * Wait for the server on-line.
     *
     * It calls "icx_getNetworkInfo" to check whether the server is on-line
     * @param url Server URL for API endpoint. It uses "<url>/api/v3" for calls
     * @return Whether it success or not
     */
    boolean waitServer(String url) {
        println("WAIT server url=${url}")
        def client = new OkHttpClient()
        def JSON = MediaType.get('application/json')
        def jsonGetNetworkInfo = "{ \"jsonrpc\": \"2.0\", \"method\": \"icx_getNetworkInfo\", \"id\": 123 }"
        def rb = RequestBody.create(JSON, jsonGetNetworkInfo)

        for (int cnt=10 ; cnt>0 ; cnt--) {
            try {
                def req = new Request.Builder().url(url).post(rb).build()
                def res = client.newCall(req).execute()
                if (res.code() == 200) {
                    println("WAIT done")
                    return true
                }
            } catch (Throwable th) {
                logger.info("WAIT exception th=${th}")
            }
            sleep(1000)
        }
        return false
    }

    synchronized void stopServer() {
        if (process != null && process.isAlive()) {
            logging.println("STOPPING Stand-alone server")
            process.destroy()
            process.waitFor(1, TimeUnit.SECONDS)
            process = null
        }
    }

    synchronized void stopContainer(boolean remove) {
        ["docker", "stop", "gochain-${dockerEnv}"].execute(null, getWorkingDir()).waitFor()
        if (remove) {
            ["docker", "rm", "gochain-${dockerEnv}"].execute(null, getWorkingDir()).waitFor()
        }
    }

    synchronized void stopDocker() {
        if (process != null && process.exitValue() == 0) {
            logging.println("STOPPING docker container")
            stopContainer(false)
            process = null
        }
    }
}

task testPyGov(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "pyGov"
    }
    def audit = System.getProperty("AUDIT")
    if (audit == "true") {
        configFile = "./data/config/pygov.json"
        chainDir = ".chain/pyaudit"
        logFile = ".chain/pyaudit.log"
        gsDir = "./data/genesisStorage/genesis_audit.json"
        envFile = "./data/chainenv/pygov.props"
        dockerEnv = "pyaudit"
    } else {
        configFile = "./data/config/pygov.json"
        chainDir = ".chain/pygov"
        logFile = ".chain/pygov.log"
        gsDir = "./data/genesisStorage/genesis_gov.json"
        envFile = "./data/chainenv/pygov.props"
        dockerEnv = "pygov"
    }

    dependsOn 'cleanTestPyGov'
    testLogging.showStandardStreams = true
}

task testPyScore(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "pyScore"
    }
    configFile = "./data/config/py.json"
    chainDir = ".chain/pyscore"
    logFile = ".chain/pyscore.log"
    gsDir = "./data/genesisStorage/genesis_py.json"
    envFile = "./data/chainenv/py.props"
    dockerEnv = "pyscore"

    dependsOn 'cleanTestPyScore'
    testLogging.showStandardStreams = true

    def forks = System.getProperty("FORKS")
    if (forks != null && forks.isInteger()) {
        maxParallelForks = Integer.valueOf(forks)
    }
}

task testJavaScore(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "javaScore"
    }
    configFile = "./data/config/java.json"
    chainDir = ".chain/javascore"
    logFile = ".chain/javascore.log"
    envFile = "./data/chainenv/java.props"
    dockerEnv = "javascore"

    dependsOn 'cleanTestJavaScore'
    testLogging.showStandardStreams = true
}

task testJavaGov(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "javaGov"
    }
    configFile = "./data/config/javagov.json"
    chainDir = ".chain/javagov"
    logFile = ".chain/javagov.log"
    gsDir = "./data/genesisStorage/genesis_javagov.json"
    envFile = "./data/chainenv/javagov.props"
    dockerEnv = "javagov"

    dependsOn 'cleanTestJavaGov'
    testLogging.showStandardStreams = true
}

task test(overwrite: true) {
    dependsOn 'testPyScore', 'testPyGov', 'testJavaScore', 'testJavaGov'
}
